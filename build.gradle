plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.0.3.RELEASE'
}

apply plugin: 'io.spring.dependency-management'

group 'pro.hirooka'
version '0.0.1-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_10
targetCompatibility = JavaVersion.VERSION_1_10

repositories {
    mavenCentral()
}

ext {
    // HLS
    bcprovVersion = '1.60'
    commonsTextVersion = '1.4'
    commonsIoVersion = '2.6'

    // UI
    bootstrapVersion = '4.1.1'
    jQueryVersion = '3.3.1'
    popperJsVersion = '1.14.3'

    // API
    springfoxSwaggerVersion = '2.8.0'

    jaxbApiVersion = '2.3.0'
    junitVersion = '4.12'
}

dependencies {
    // Spring
    compile ('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    compile 'org.springframework.boot:spring-boot-starter-jetty'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0' // for Jetty
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4'
    compile 'org.springframework.data:spring-data-mongodb'
    compile 'org.springframework.boot:spring-boot-devtools'
    testCompile 'org.springframework.boot:spring-boot-starter-test'

    // HLS
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: bcprovVersion
    compile group: 'org.apache.commons', name: 'commons-text', version: commonsTextVersion
    compile group: 'commons-io', name: 'commons-io', version: commonsIoVersion // EPG too

    // UI
    compile group: 'org.webjars', name: 'bootstrap', version: bootstrapVersion
    compile group: 'org.webjars', name: 'jquery', version: jQueryVersion
    compile group: 'org.webjars.npm', name: 'popper.js', version: popperJsVersion

    // API
    compile group: 'io.springfox', name: 'springfox-swagger2', version: springfoxSwaggerVersion
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: springfoxSwaggerVersion

    // Test
    testCompile group: 'junit', name: 'junit', version: junitVersion

    compile group: 'javax.xml.bind', name: 'jaxb-api', version: jaxbApiVersion // Spring Boot 2 + Java 10 + JPA workaround
}

bootJar {
    launchScript()
}